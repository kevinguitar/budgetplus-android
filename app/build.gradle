plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

ext {
    compose_ui_version = '1.2.0'
    compose_compiler_version = '1.2.0'
    nav_version = '2.5.1'
    dagger_hilt_version = '2.42'
}

android {
    namespace 'com.kevlina.budgetplus'
    compileSdk 32

    defaultConfig {
        applicationId "com.kevlina.budgetplus"
        minSdk 23
        targetSdk 32
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file("/Users/kevin/Documents/BudgetPlus/BudgetKey")
            storePassword 'budget+'
            keyAlias 'key0'
            keyPassword 'budget+'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion project.ext.compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}

kapt {
    correctErrorTypes true
}

dependencies {

    // Android core
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // Jetpack Compose
    // https://developer.android.com/jetpack/androidx/releases/compose-ui
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation "androidx.compose.material:material:$compose_ui_version"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    // Compose Compiler
    // https://developer.android.com/jetpack/androidx/releases/compose-compiler
    implementation "androidx.compose.compiler:compiler:$compose_compiler_version"
    // Compose UI utils
    // https://google.github.io/accompanist/flowlayout/
    implementation "com.google.accompanist:accompanist-flowlayout:0.24.9-beta"

    // Lottie
    // https://github.com/airbnb/lottie/blob/master/android-compose.md
    implementation "com.airbnb.android:lottie-compose:5.2.0"

    // Kotlinx serialization
    // https://github.com/Kotlin/kotlinx.serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"

    // DateStore
    // https://developer.android.com/topic/libraries/architecture/datastore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Navigation
    // https://developer.android.com/jetpack/androidx/releases/navigation
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Dagger Hilt
    // https://dagger.dev/hilt/gradle-setup
    implementation "com.google.dagger:hilt-android:$dagger_hilt_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_hilt_version"

    // Firebase
    // https://firebase.google.com/support/release-notes/android
    implementation platform('com.google.firebase:firebase-bom:30.3.1')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // 3rd party auth
    implementation 'com.google.android.gms:play-services-auth:20.2.0'
    implementation 'com.facebook.android:facebook-login:13.0.0'

    // AdMob
    implementation 'com.google.android.gms:play-services-ads:21.1.0'

    // Utils
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'net.objecthunter:exp4j:0.4.8'
    implementation 'io.coil-kt:coil-compose:2.1.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"

}